dnl ########################################################################
dnl #
dnl #  (C) 2002-2008
dnl #    Henning P. Schmiedehausen
dnl #    INTERMETA - Gesellschaft fuer Mehrwertdienste mbH
dnl #    Hutweide 15
dnl #    D-91054 Buckenhof
dnl #  (C) 2010-2013
dnl #    Elan Ruusam√§e, <glen@delfi.ee>
dnl #
dnl ########################################################################

AC_INIT([nagios-snmp-plugins], 1.3.1)
AC_CONFIG_SRCDIR([check_snmp_disk.c])
AM_INIT_AUTOMAKE

AM_CONFIG_HEADER(config.h)

dnl same as nagios configure.in
AC_PREFIX_DEFAULT(/usr/local/nagios)

dnl Figure out how to invoke "install" and what install options to use.

AC_PROG_INSTALL
AC_SUBST(INSTALL)
AC_CHECK_HEADERS(stdlib.h unistd.h string.h getopt.h errno.h)

AC_PROG_CC

LIBS=""
INCLUDE=""

save_LIBS="$LIBS"
LIB_SOCKET=""
AC_CHECK_FUNC(connect, :,
	[AC_CHECK_LIB(nsl, gethostbyname,
		LIB_SOCKET="-lnsl $LIB_SOCKET")]
	[AC_CHECK_LIB(socket, connect,
		LIB_SOCKET="-lsocket $LIB_SOCKET")]
)
LIBS="$LIB_SOCKET $save_LIBS"
AC_CHECK_FUNC(res_search, :,
	[AC_CHECK_LIB(resolv, res_search,
		LIB_SOCKET="-lresolv $LIB_SOCKET")]
)
LIBS="$LIB_SOCKET $save_LIBS"
AC_CHECK_FUNCS(dn_expand dns_lookup)
LIBS="$save_LIBS"
AC_SUBST(LIB_SOCKET)


AC_ARG_WITH(snmp,
            [  --with-snmp=DIR      use snmp (rooted in DIR) [yes] ],
            with_snmp=$withval, with_snmp=yes)

SNMP_CONFIG=""
if test "$with_snmp" != no; then
  AC_PATH_PROG(SNMP_CONFIG,net-snmp-config,,[/usr/local/bin:$PATH])

  if test -x "$SNMP_CONFIG"; then
    SNMP_LIBS=`$SNMP_CONFIG --netsnmp-libs`
    SNMP_LIBS="$SNMP_LIBS `$SNMP_CONFIG --external-libs`"
    SNMP_PREFIX=`$SNMP_CONFIG --prefix`

    if test -n "$SNMP_LIBS" && test -n "$SNMP_PREFIX"; then
      INCLUDE="${INCLUDE} ${SNMP_PREFIX}/include"
      LIBS="${LIBS} $SNMP_LIBS"
      AC_DEFINE(HAVE_NET_SNMP,1,[ ])
      SNMP_LIBNAME=netsnmp
    else
      AC_MSG_ERROR([Could not find the required paths. Please check your net-snmp installation.])
    fi
  else
    AC_MSG_ERROR([Could not find net-snmp-config. Please check your net-snmp installation.])
  fi
fi

AC_SUBST(LIB_SNMP)

dnl lllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllllll
dnl Set exec_prefix, bindir, libdir, includedir to some sensible values
# expand prefix and pass it to subsequent configures
if test "x$prefix" = xNONE; then
	prefix=${ac_default_prefix}
	ac_configure_args="$ac_configure_args --prefix=${prefix}"
fi
# expand exec_prefix
if test "x$exec_prefix" = xNONE; then
	exec_prefix=${prefix}
	ac_configure_args="$ac_configure_args --exec_prefix=${exec_prefix}"
fi
# expand bindir
if test "x$bindir" = 'x${exec_prefix}/bin'; then
	bindir="${exec_prefix}/bin"
	ac_configure_args="$ac_configure_args --bindir=${bindir}"
fi
plugindir=$bindir
AC_SUBST([plugindir], $plugindir)

AC_OUTPUT([Makefile nagios-snmp-plugins.cfg nagios-snmp-plugins.spec])
